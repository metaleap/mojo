# Milestone 01: Infancy.

## Atmo user language:

- functions:
  - nullary to n-ary
  - recursion
  - but no closures, partial application, or higher order yet
- operators:
  - comparison: `==`
  - branching: `?` with `|`
- types:
  - "tags": like Lisp's symbols, Erlang's atoms etc.
    - any `#tagged` token becomes an expression of "_tag_ shape"
    - tags `#true` and `#false` are produced by the comparison operators and accepted by conditionals

## Atmo core language:

- types:
  - `@Sym`
    - used for tags
    - storage spec: `int` that's `unsigned` and with `bit-width` sized for number of unique tags occurring in program
      - we have no notion of "int"s yet (or signed-ness), other than as a moniker we know our low-level compilation targets understand as a sized notion for storage
  - `@Void`
    - necessary even now for CPS transform
  - `@Fn`
    - hardwired "minimal higher-order notion" for CPS, but not in user language yet
- ops:
  - `@iCmpEq (lhs:@Sym, rhs:@Sym) -> @Sym(#true|#false)`
  - `@if (scrut:@Sym(#true|#false), if_true:@Sym, if_false:@Sym -> @Sym`
  - `@tmpRndSym () -> @Sym(/=#true & /=#false)`
    - temporary helper (will be removed later on)
      - to get some quasi-nondeterministic "runtime-only" not-statically-reducable value-producer in from the start
    - generates runtime `rand()` call within range of program's full set of symbols excluding both `#true` and `#false`
  - (notations above for prim-op args / returns is ad-hoc and sub-precise and not of-the-language because our type system isn't ripe enough yet)

## Scenarios:

Given `milestone01.at`:

```swift
isWeekend1 day :=
  day ? #sun  =>  #true   // no sugar yet for multiple ..
      | #sat  =>  #true   //  .. comparees in single case
      | _     =>  #false

isWeekend2 day :=         // no ternary-sugar yet
  (day == #sun) ? #true   =>  #true // no `||` yet
                | #false  =>  (day == #sat)

isWeekend3 day :=         // sour, but sugar comes later
  day ? #sun  =>  #true
      | #sat  =>  #true
      | #mon  =>  #false
      | #tue  =>  #false
      | #sat  =>  #true   // marked unreachable
      | #wed  =>  #false
      | #thu  =>  #false
      | #fri  =>  #false

isWeekday0 day := not (isWeekend3 day)

isWeekday1 day :=
  isWeekend3 foo // error: 'isWeekend3' undefined for 'day' values of 'foo'
  foo := isWeekend2 day

notEq tag1 tag2 :=    // no `/=` sugar yet
  not (tag1 == tag2)  // asserts set-overlap between operands

not b :=
  b ? #true => #false | #false => #true // no ternary-sugar yet

not2 b :=
  b ? not #false  => not #true
    | #true       => #false    // marked unreachable
    | #true       => #true     // error: contradictory to line 31

rndDay1 weekend :=
  isWeekend3 rnd_day  ? weekend => rnd_day
                      | _       => rndDay1 weekend
  rnd_day := @tmpRndSym ()

rndDay2 weekend :=
  not (@tmpRndSym ()) // error: 'not' undefined for 'b' values of '@tmpRndSym ()'

rndDay3 weekend :=
  #false == (@tmpRndSym ()) // error: no set-overlap of operands
    ? #true => rndDay2 weekend
    | _     => rndDay1 weekend

rndDay4 weekend :=
  notEq (isWeekend3 (rndDay1 weekend)) (@tmpRndSym ()) // error: no overlap between '(isWeekend3 (rndDay1 weekend))' values and '@tmpRndSym ()' values

rndDay5 weekend := // offer: simplify to `rndDay5 := rndDay1`
  #false
    ? #true  => rndDay2 weekend // marked unreachable
    | _      => rndDay1 weekend
```

... we want the core lang to "spec" these functions out roughly as follows (only some examples, not all functions covered).

### `isWeekend3`:
  - `day` assertion: `==#sun || ==#sat || ==#mon || ==#tue || ==#wed || ==#thu || ==#fri`
  - return guarantee: `==#true || ==#false`

(Again, ad-hoc syntax not of-the-language yet, there's no actual `||` for one.)

Actually must come out much more refined than outlined above. The "spec" produced would actually have to be (symbolically / approximately) as detailed as the definition itself &mdash; in fact here exactly-equal because all values are statically-known, however in more runtime-known-values-dominant scenarios still more verbose than the code itself, as all accumulated factoids (tree of predicates) are captured per each individual expression &mdash; but for now let's first digest this "simpler spec-ing" formulated above as a first rough idea of what's in store eventually.

### `isWeekend2` and `isWeekend3`:

  - return guarantee: `==#true || ==#false`
  - `day` assertion: `@Any`

Note that in this milestone, `@Any` is equivalent to `@Sym`, which for now is the entirety of the (user-accessible) type universe.

This style of generic-catch-all will eventually be discouraged once cleaner more restrictive and more precise formulations become, through sugars, even more convenient for the programmer than adding a catch-all default match.

### `isWeekday`:

  - `foo` assertion: `==#true || ==#false`
  - `day` assertion: `@Any`
  - return guarantee: (invalid)
    - could lazily just propagate `isWeekend3`'s return guarantee for now.. but should get this "unspecable" diagnosis in from the start

## Sema = abstract-interp/partial-eval

The above must and can really only be the outcome of ongoing atmo-tailored live semantic analysis (sema) that presumably approximately implements (to our muddy pragmatic evolving understanding) some custom mash-up of "abstract/symbolic interpretation" and-or "partial evaluation".

Consider first that most-all real-world functions (excepting a handful of FP-style simpletons like `id`/`const` aka. "argument selectors" incl. the countless Church-encoding-likes) call other functions and/or prim-ops (whose "specs" are pre-defined / hardwired / axiomatic / non-derived).

It is to be understood that during sema, any function call encountered instantiates a "call-specific function specialization" expansion of the callee. Thus we traverse depth-first into the graph until reaching some "leaf functions": all those that lack function calls themselves.

- Reach a **leaf function**:
  - regardless of the caller from which we just first reached it, immediately the "generic baseline instance" (ie. _no actual "specialization" at all_ in the usual meaning of the phrase) is ensured &mdash; one where all args are considered runtime-known / statically-unknown.
    - still there might be statically reducable exprs, they are immediately reduced
    - usage of args with prim-ops determines their first "facts" / assertions / constraints
    - determine the set of arg-set/return-set combinations
- Back up to the callsite in non-leaf function:
  - Add now-available-from-the-callee constraints on all arg exprs to the set of whatever constraints they already had
- Once all constraints on all sub-expressions gathered:
  - Only now check for contradictions / impossibilities
  - Gather calls that can be specialized: those where any arg expr has more detailed constraints than the callee's "generic baseline instance" asserts
  - Instantiate specializations: track if any change from baseline in structure or in arg/ret asserts, refine exprs affected by the switch to the specialized-instance, reprocess what's affected by _those_ refinements, etc.

This all must be in the form of a node-walking evaluator with:

  - added support for "hole values" that have the "interpretative effect" of accumulating predicates / assertions on that expr
  - online checks for violations of the predicates established on sub-exprs wrt their uses
    - similar to how most interpreted languages catch-and-throw type errors "at runtime" (eval time), this evaluator likewise does at _its_ "interpretation time" which happens to be our "static semantic analysis time"

# Walkthroughs: `milestone01.at`

## Leaf func: `isWeekend1`

To recap, user source:

```swift
isWeekend1 day :=
  day ? #sun  =>  #true   // no sugar yet for multiple ..
      | #sat  =>  #true   //  .. comparees in single case
      | _     =>  #false
```

IL, initially:

```swift
  isWeekend1 := day ->
    @if (@iCmpEq day #sun)  #true
                            (@if (@iCmpEq day #sat) #true
                                                    #false)
```

IL, initial CPS:

- for now, we can be simple-minded: `@if`s turn into `@br`s
  - and so we make 2 nullary labels for the 2 cases
    - they must `ret` for atoms or "`@br`anch out again" for `@if`s

```swift
isWeekend1 day ret:
  @br (@iCmpEq day #sun) day_eq_sun_yay day_eq_sun_nay
day_eq_sun_yay:
  ret #true
day_eq_sun_nay:
  @br (@iCmpEq day #sat) day_eq_sat_yay day_eq_sat_nay
day_eq_sat_yay:
  ret #true
day_eq_sat_nay:
  ret #false
```

IL sema:

- with `day = ?` in `isWeekend1`, on `@br`:
  - `(@iCmpEq day #sun)`: fact-up on `day`: `@Sym`s not excluding `#sun`
  - `day_eq_sun_yay`:
    - fact-up on `ret`: not undefined for `#true`
    - fact-down on `day`: only ever `#sun`
  - `day_eq_sun_nay`, on `@br`:
    - fact-down on `day`: never `#sun`
    - `(@iCmpEq day #sat)`: fact-up `day`: `@Sym`s not excluding `#sat`
    - `day_eq_sat_yay`:
      - fact-down on `day`: only ever `#sat`
      - fact-up on `ret`: not undefined for `#true`
    - `day_eq_sat_nay`:
      - fact-down on `day`: never `#sat`
      - fact-up on `ret`: not undefined for `#false`
- ret-arg rels
  - `ret #true`: `day == #sat || day == #sun`
  - `ret #false`: `day /= #sat && day /= #sun`

## Leaf func: `isWeekend2`

To recap, user source:

```swift
isWeekend2 day :=         // no ternary-sugar yet
  (day == #sun) ? #true   =>  #true // no `||` yet
                | #false  =>  (day == #sat)
```

IL initially, in theory:

```swift
isWeekend2 := day ->
  @if (@iCmpEq (@iCmpEq day #sun) #true) #true (
    @if (@iCmpEq (@iCmpEq day #sun) #false) (@iCmpEq day #sat) @never
  )
```

Later we show that "smart node constructors" return direct result nodes for immediately computable prim-ops,
so "in practice" even the initial (non-CPS'd) IL would have rewritten much of the above from these rules:

- `(@iCmpEq $foo #true)` / `(@iCmpEq #true $foo)` &rarr; `$foo`
- `(@if (@iCmpEq $foo #false) $then $else)` / `(@if (@iCmpEq #false $foo) $then $else)` &rarr; `(@if $foo $else $then)`

but let's postpone that just til the milestone01 end to ensure this is still captured in the sema phase where
new suddenly-computable / now-statically-known terms may be produced much later than initial constructions.

IL, goal-state CPS:

```swift
isWeekend2 day ret:
  @br (@iCmpEq (@iCmpEq day #sun) #true) day_eq_sun_true_yay day_eq_sun_true_nay
day_eq_sun_true_yay:
  ret #true
day_eq_sun_true_nay:
  @br (@iCmpEq (@iCmpEq day #sun) #false) day_eq_sun_false_yay @never
day_eq_sun_false_yay:
  ret (@iCmpEq day #sat)
```

IL sema:

- with `day = ?` in `isWeekend1`, on `@br`:
  - `(@iCmpEq (@iCmpEq day #sun) #true)`:
    - rewrite pattern: `(@iCmpEq $foo #true) == (@iCmpEq #true $foo) == $foo`
  - `(@iCmpEq day #sun)`:
    - fact-up `day`: `@Sym`s not excluding `#sun`
  - `day_eq_sun_true_yay`:
    - fact-down `day`: only ever `#sun`
    - fact-up `ret`: not undefined for `#true`
  - `day_eq_sun_true_nay`, on `@br`:
    - fact-down `day`: never `#sun`
    - rewrite pattern: `(@br (@iCmpEq $foo #false) $then $else) == (@br (@iCmpEq #false $foo) $then $else) == (@br $foo $else $then)`
      - `(@iCmpEq day #sun)`: already handled
    - rewrite patterns: `(@br $foo $bar @never)` / `(@br $foo @never $bar)`:
      - fact on `$foo`: always only ever equal to (`#true`/`#false` depending on `@never` placement)
      - replace whole `@br` with jump to `$bar`
      - fact-up `day`: never `#sun`
    - `day_eq_sun_false_yay`:
      - `(@iCmpEq day #sat)`: fact-up `day`: `@Sym`s not excluding `#sat`
      - fact `ret`: not undefined for `#true` and not undefined for `#false`
- ret-arg rels:
  - `ret #true`: `day == #sun || day == #sat`
  - `ret #false`: `day /= #sun && day /= #sat`

## Leaf func: `isWeekend3`

User source:

```swift
isWeekend3 day :=
  day ? #sun  =>  #true
      | #sat  =>  #true
      | #mon  =>  #false
      | #tue  =>  #false
      | #sat  =>  #true
      | #wed  =>  #false
      | #thu  =>  #false
      | #fri  =>  #false
```

IL, initially:

```swift
isWeekend3 := day ->
  @if (@iCmpEq day #sun) #true (
    @if (@iCmpEq day #sat) #true (
      @if (@iCmpEq day #mon) #false (
        @if (@iCmpEq day #tue) #false (
          @if (@iCmpEq day #sat) #true (
            @if (@iCmpEq day #wed) #false (
              @if (@iCmpEq day #thu) #false (
                @if (@iCmpEq day #fri) #false @never)))))))
```

IL, CPS'd:

```swift
isWeekend3 day ret:
  @br (@iCmpEq day #sun) day_eq_sun_yay day_eq_sun_nay
day_eq_sun_yay:
  ret #true
day_eq_sun_nay:
  @br (@iCmpEq day #sat) day_eq_sat_yay day_eq_sat_nay
day_eq_sat_yay:
  ret #true
day_eq_sat_nay:
  @br (@iCmpEq day #mon) day_eq_mon_yay day_eq_mon_nay
day_eq_mon_yay:
  ret #false
day_eq_mon_nay:
  @br (@iCmpEq day #tue) day_eq_tue_yay day_eq_tue_nay
day_eq_tue_yay:
  ret #false
day_eq_tue_nay:
  @br (@iCmpEq day #sat) day_eq_sat_yay2 day_eq_sat_nay2
day_eq_sat_yay2:
  ret #true
day_eq_sat_nay2:
  @br (@iCmpEq day #wed) day_eq_wed_yay day_eq_wed_nay
day_eq_wed_yay:
  ret #false
day_eq_wed_nay:
  @br (@iCmpEq day #thu) day_eq_thu_yay day_eq_thu_nay
day_eq_thu_yay:
  ret #false
day_eq_thu_nay:
  @br (@iCmpEq day #fri) day_eq_fri_yay @never
day_eq_fri_yay:
  ret #false
```

IL sema:

- same proceeding as earlier, but hitting the 2nd `@br (@iCmpEq day #sat)`:
  - from the 1st occurrence, we got handed fact-down `day /= #sat` here
    - on entry `day` was statically-unknown, but here / by now: statically-semi-known
  - so we have `@br #false day_eq_sat_yay2 day_eq_sat_nay2`
  - rewrite to jump to `day_eq_sat_nay2`
  - now no reference to node `day_eq_sat_yay2`, refcount decremented to 0
  - we still (before the rewrite) sema'd into it: if there were eg. a `ret #false`,
    contradiction would be facted (error), but _here_ nothing was changed from before

## Leaf func: `not`

User source:

```swift
not b :=
  b ? #true => #false | #false => #true // no ternary-sugar yet
```

IL initially, in theory:

```swift
not := b ->
  @if (@iCmpEq b #true) #false
                        (@if (iCmpEq b #false) #true @never)
```

IL initially, in practice first:

```swift
not := b ->
  @if (@iCmpEq b #true) #false (@if b @never #true)
```

IL initially, in practice second:

```swift
not := b ->
  @if b #false (@if b @never #true)
```

Note for IL node construction-time immediate-rewrites, whether `@never` propagates immediately is decided
by the prim-op. For `@iCmpEq` it would propapate, for `@if` (in _cases_) with unknown-cond it won't.

IL, CPS'd:

```swift
not b ret:
  @br b b_eq_true_yay b_eq_true_nay
b_eq_true_yay:
  ret #false
b_eq_true_nay:
  @br b @never b_eq_true_nay2
b_eq_true_nay2:
  ret #true
```

Rewrite `@br $foo @never $bar` to jump to `$bar`:

```swift
not b ret:
  @br b b_eq_true_yay b_eq_true_nay
b_eq_true_yay:
  ret #false
b_eq_true_nay:
  b_eq_true_nay2
b_eq_true_nay2:
  ret #true
```

The "senseless indirection" of `b_eq_true_nay` to `b_eq_true_nay2` does not
really exist as all nodes are uniqued by construction: there are no names or
referencers-to-names but simply pointers to nodes. So the above is in reality:

```swift
not b ret:
  @br b b_eq_true_yay b_eq_true_nay
b_eq_true_yay:
  ret #false
b_eq_true_nay:
  ret #true
```

As noted earlier on `@never` elimination, the fact-findings from the cond for
the surviving case are facted-up-and-down. So the above form "lost" the
knowledge encoded in the earlier forms that `b` can only be `#true | #false`
and nothing else, but it was already captured.

(It is understood that once the IL acquires a notion of logical operators, we must eventually
figure out how and _when_ to rewrite "branchings like these" into equivalent bin-ops. Fairly
trivial for the statically pre-defined int-reprs of `#false` and `#true` as `0` and `1`, but
interesting even to analyze for opportunities elsewhere on arbitrary #tag branchings, as the
IL fixes all their int-reprs early. Which veers into very-late-stage nice-to-have distractions.)

## Non-leaf func: `isWeekday0`

User source:

```swift
isWeekday0 day :=
  not (isWeekend3 day)
```

IL initially:

```swift
isWeekday0 := day ->
  not (isWeekend3 day)
```

IL CPS'd:

```swift
isWeekday day ret:
  isWeekend3 day into_not
into_not b1:
  not b1 out_of_not:
out_of_not b2:
  ret b2
```

No specializations / instantiations during sema:

- nothing known about `day`, so `isWeekend3` base instance ref'd by call
- `b1` has _some_ static facts (from `isWeekend3` facts on its `ret`),
  but they don't expand upon the arg facts of the `not` base instance

## Non-leaf func: `isWeekday1`

User source:

```swift
isWeekday1 day :=
  isWeekend3 foo // error: 'isWeekend3' undefined for 'day' values of 'foo'
  foo := isWeekend2 day
```

IL initially:

```swift
isWeekday1 := day ->
  isWeekend3 (isWeekend2 day)
```

IL, CPS'd:

```swift
isWeekday1 day ret:
  isWeekend2 day isWeekend2_day
isWeekend2_day foo:
  isWeekend3 foo ret
```

Sema goals outline:

- from `isWeekend2` base instance:
  - fact-up: sets for `day` never exclude `#sat` or `#sun`
  - `foo` can only ever be `#true | #false`
- instantiate `isWeekend3` for above `foo` facts: they're not in base instance
  - this "inlines" (conceptually) the graph and must reduce to the resulting `@never`
- now that a top-level / non-local def is `@never`, fair to flag _it_ rather than
  all its call-sites
  - fact-ups from this now-invalid def won't propagate to call-sites
  - but the `@never`ness itself does.

It's a chore but let's spell it out. First we had, to repeat from above:

```swift
isWeekday1 day ret:
  isWeekend2 day isWeekend2_day
isWeekend2_day foo:
  isWeekend3 foo ret
```

"Inlined" / instantiated `isWeekend3`, in readable notation:

```swift
isWeekday1 day ret:
  isWeekend2 day isWeekend2_day
isWeekend2_day foo:
  isWeekend3Inst
isWeekend3Inst:
  @br (@iCmpEq foo #sun) day_eq_sun_yay day_eq_sun_nay
  // --abrupt interruption here.
```

We can stop already without further expansion. As a now-decided ground rule: while
we still will sema both cases of a `@br` even if the cond is statically-known
`#true | #false` (ie. legal) to catch errors even for (currently) unreachable paths,
if the cond is "anything-else" and thus facted `@never`, it's neither invalid nor
degenerative behavior to break out of "spezialization-sema" immediately.

Why is the cond here `@never`? It is prescribed by `@iCmpEq` for `@Sym`s that
the statically-known-to-here set of one operand must not exclude any members of
the statically-known-to-here set of the other operand, if any of the two operand
sets are statically-known-to-here to house more than one member. In our _instance_
`foo`'s set of `#true | #false` is known to exclude _all other_ `@Sym`s incl. `#sun`.

## Non-leaf func: `notEq`

User source:

```swift
notEq tag1 tag2 :=
  not (tag1 == tag2)
```

IL initially:

```swift
notEq := tag1 tag2 ->
  not (@iCmpEq tag1 tag2)
```

IL, CPS'd:

```swift
notEq tag1 tag2 ret:
  not (@iCmpEq tag1 tag2) ret
```

IL, sema goals:

- Won't instantiate `not`:
  - `(@iCmpEq tag1 tag2)` ret-facts don't expand upon `not` base-instance's arg-facts
- fact-ups on `tag1` and `tag2`: from `@iCmpEq` arg-facts
  - ie. "both operands' sets must not exclude each other"
- fact-up on `ret`: from `not` ret-facts

## Non-leaf func: `not2`

User source:

```swift
not2 b :=
  b ? not #false  => not #true
    | #true       => #false    // marked unreachable
    | #true       => #true     // error: contradictory
```

IL initially, in theory:

```swift
not2 := b ->
  @if (@iCmpEq b (not #false)) (not #true) (
      @if (@iCmpEq b #true) #false (
          @if (@iCmpEq b #true) #true @never
        )
    )
```

IL initially, in practice:

```swift
not2 := b ->
  @if (@iCmpEq b (not #false))  (not #true)
                                (@if b #false (@if b #true @never))
```

IL, CPS'd:

```swift
not2 b ret:
  not #false _not_false
_not_false not_false:
  @br (@iCmpEq b not_false) b_eq_not_false_yay b_eq_not_false_nay
b_eq_not_false_yay:
  not #true ret
b_eq_not_false_nay:
  @br b b_eq_true_yay b_eq_true_nay
b_eq_true_yay:
  ret #false
b_eq_true_nay:
  @br b b_eq_true_yay2 @never
b_eq_true_yay2:
  ret #true
```

IL sema goals:

- `not #true ret` &rarr; `ret #false`
- `not_false == #true` hence `(@iCmpEq b not_false) == (@iCmpEq b #true)`
- hence ``@br (@iCmpEq b #true) ...` &rarr; `@br b ...`
- the above is first encounter of `@br b ...`, two more follow:
  - as hinted in an earlier example, here we have it: latter contradicts and former doesn't
    - latter must become error and former merely annotated unreachable

## Non-leaf rec func: `rndDay1`

User source:

```swift
rndDay1 weekend :=
  isWeekend3 rnd_day  ? weekend => rnd_day
                      | _       => rndDay1 weekend
  rnd_day := @tmpRndSym ()
```

IL initially:

```swift
rndDay1 := weekend ->
  (rnd_day ->
      @if (@iCmpEq (isWeekend3 rnd_day) weekend)  rnd_day
                                                  (rndDay1 weekend)
    ) (@tmpRndSym ())
```

The top-level recursing won't trouble us for this milestone.

IL, CPS'd:

```swift
rndDay1 weekend ret:
  _rnd_day (@tmpRndSym())
_rnd_day rnd_day:
  isWeekend3 rnd_day _isWeekend3_rnd_day
_isWeekend3_rnd_day isWeekend3_rnd_day:
  @br (@iCmpEq isWeekend3_rnd_day weekend) ret_rnd_day ret_rndDay1_weekend
ret_rnd_day:
  ret rnd_day
ret_rndDay1_weekend:
  rndDay1 weekend ret
```

It's tempting to "easily en-loop at least this very trivial kind of recursing" but let's
just hold our horses and tackle the topic to full exhaustion in the next milestone.

However, sema still needs to detect and not-loop-into it, obviously. For now we simply
accept that by necessity all facts gathered for `weekend` and `ret` (applicable to
all `rndDay1` call-sites) both apply to, and still hold at, the own recursing call-site.
For now: cross-check them (call-site vs. now-considered-done-callee), error on mismatch.

## Non-leaf func: `rndDay2`

User source:

```swift
rndDay2 weekend :=
  not (@tmpRndSym ()) // error: 'not' cannot handle '@tmpRndSym ()' values
```

IL initially:

```swift
rndDay2 := weekend ->
  not (@tmpRndSym ())
```

IL, CPS'd:

```swift
rndDay2 weekend ret:
  not (@tmpRndSym()) ret
```

Sema has no surprises here.

## Non-leaf func: `rndDay3`

User source:

```swift
rndDay3 weekend :=
  #false == (@tmpRndSym ()) // error: comparing intrinsically-incompatible operand sets
    ? #true => rndDay2 weekend
    | _     => rndDay1 weekend
```

IL, initially:

```swift
rndDay3 := weekend ->
  @if (@iCmpEq (@iCmpEq #false (@tmpRndSym ())) #true)  (rndDay2 weekend)
                                                        (rndDay1 weekend)
```

IL, CPS'd:

```swift
rndDay3 weekend ret:
  @br (@iCmpEq (@iCmpEq #false (@tmpRndSym ())) #true) yay nay
yay:
  rndDay2 weekend ret
nay:
  rndDay1 weekend ret
```

Sema: the inner `@iCmpEq` becomes `@never` and so does `rndDay3` itself.

## Non-leaf func: `rndDay4`

User source:

```swift
rndDay4 weekend :=
  notEq (isWeekend3 (rndDay1 weekend)) (@tmpRndSym ()) // error: comparing intrinsically-incompatible operand sets
```

IL initially:

```swift
rndDay4 := weekend ->
  notEq (isWeekend3 (rndDay1 weekend)) (@tmpRndSym ())
```

IL, CPS'd:

```swift
rndDay4 weekend ret:
  rndDay1 weekend andThen1
andThen1 rndDay1_weekend:
  isWeekend3 rndDay1_weekend andThen2
andThen2 isWeekend3_rndDay1_weekend:
  notEq isWeekend3_rndDay1_weekend (@tmpRndSym ()) ret
```

Sema: `notEq (#true|#false) (#sat..#fri)` specialization produces (and propagates) `==` error.

## Non-leaf func: `rndDay5`

User source:

```swift
rndDay5 weekend := // offer: simplify to `rndDay5 := rndDay1`
  #false
    ? #true  => rndDay2 weekend // marked unreachable
    | _      => rndDay1 weekend
```

IL initially, in theory:

```swift
rndDay5 := weekend ->
  @if (@iCmpEq #false #true)  (rndDay2 weekend)
                              (rndDay1 weekend)
```

but thanks to "preducing node constructors", trivially-immediately-computable
prim-op constructions return pointers to result nodes, so in reality:

```swift
rndDay5 := rndDay1
```

IL-CPS'd hence is the one from `rndDay1`.

This highlights a subtlety about un/reachability annotations, they may be
modified during sema but are _initialized_ already during node construction.
